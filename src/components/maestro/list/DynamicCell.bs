function init()
  m.cell = invalid
  m.top.cell = invalid
  m.cellProvider = m.global.cellProvider
  m.cellTracker = m.global.cellTracker
  m.cellTracker@.bindCellToContent(m.top)
  m.viewState = "hidden"
  m.itemContent = invalid
  m.top.observeField("viewState", "onViewStateChange")
end function

function itemContentChanged()
  ? "itemContentChanged : " ; m.top.index ; m.top.trackingId
  m.itemContent = m.top.itemContent
  m.cellTracker@.bindCellToContent(m.top, m.itemContent)
  if m.itemContent <> invalid
    m.viewState = m.cellTracker@.getStateForContentId(m.itemContent.id)
  end if
  updateCell() 
end function

function updateCell() as void
  if m.viewState <> "show"
    ? "DC cell not shown: RI" ; m.top.index ; " CI "; m.top.trackingId
    return
  end if

  if m.cell <> invalid and (m.itemContent = invalid or m.cell.subType() <> m.itemContent.cellType)
    m.cellProvider@.releaseCell(m.cell) 
    ? "DC - releasing: RI" ; m.top.index ; " CI "; m.top.trackingId
    m.cell = invalid
    m.top.cell = invalid
  end if

  if m.cell = invalid
    m.cell = m.cellProvider@.getCell(m.itemContent.cellType)
    m.top.cell = m.cell
    m.top.appendChild(m.cell)
  end if

  if m.cell <> invalid

    m.cell.index = m.top.index
    m.cell.itemContent = m.itemContent
  else
    ? "DC - cell is invalid: RI" ; m.top.index ; " CI "; m.top.trackingId
  end if

end function

function onViewStateChange(evt) 
  ? "onViewStateChange"; evt.getData()
  m.viewState = evt.getData()
  if m.viewState = "show"
    updateCell()
    if m.cell <> invalid 
      m.cell.index = m.top.index
      m.cell.currTarget = m.top.currTarget
      m.cell.currRect = m.top.currRect
      m.cell.focusPercent = m.top.focusPercent
      m.cell.groupHasFocus = m.top.groupHasFocus
    end if
  else if m.viewState = "hidden" and m.cell <> invalid
    m.cellProvider@.releaseCell(m.cell) 
    m.cell = invalid
    m.top.cell = invalid
  end if
end function

function currTargetChanged()
  if m.cell <> invalid
    m.cell.currTarget = m.top.currTarget
  end if
end function

function currRectChanged()
  if m.cell <> invalid
    m.cell.currRect = m.top.currRect
  end if
end function

function onIndexChanged()
  ? "on index changed" ; m.top.index
end function

function focusPercentChanged()
  if m.cell <> invalid
    m.cell.groupHasFocus = m.top.groupHasFocus
    m.cell.focusPercent = m.top.focusPercent
  end if
end function
