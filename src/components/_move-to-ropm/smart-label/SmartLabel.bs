function init()
  m.contentGroup = m.top.findNode("contentGroup")
  m.measureLabel = createObject("roSGNode", "Label")

  m.fontFields = ["font", "header1Font", "header2Font", "header3Font", "italicFont", "boldFont"]
  m.defaultFont = getFont("MediumSystemFont", 20)
  createRegexes()
  setFontDefaults()
  for each name in m.fontFields
    m.top.observeField(name, "updateFonts")
  end for

  updateFonts()
  m.top.observeField("text", "onTextChange")
end function

function createRegexes()
  m.regexes = [
    { name: "poster": r: createObject("roRegex", "^\[\[([\s\S]*?\S)\]\]", "") }
    { name: "header3Font": r: createObject("roRegex", "^###([\s\S]*?\S)###", "") }
    { name: "header2Font": r: createObject("roRegex", "^##([\s\S]*?\S)##", "") }
    { name: "header1Font": r: createObject("roRegex", "^#([\s\S]*?\S)#", "") }
    { name: "boldFont": r: createObject("roRegex", "^\*([\s\S]*?\S)\*", "") }
    { name: "italicFont": r: createObject("roRegex", "^_([\s\S]*?\S)_", "") }
    { name: "font": r: createObject("roRegex", "(^[^_*#\[\]]*)", "") }
  ]
end function

function onTextChange()
  resetView()
  text = processTextValues()
  lines = text.split("\n")

  groups = []
  for each line in lines
    groups.push(addLine(line))
  end for

  m.contentGroup.appendChildren(groups)
end function

function processTextValues()
  text = m.top.text
  if m.top.values = invalid
    values = {}
  else 
    values = m.top.values
  end if
  for each key in values
    if (type(values[key]) = "roSGNode")
      m.posters[key] = values[key]
      text = text.replace("${" + key + "}", "[[" + key + "]]")
    else
      text = text.replace("${" + key + "}", values[key])
    end if
  end for

  return text
end function

function addLine(text)
  group = createObject("roSGNode", "LayoutGroup")
  group.layoutDirection = "horiz"
  group.horizAlignment = m.top.horizAlignment
  group.vertAlignment = m.top.vertAlignment
  parts = getLineParts(text)
  'split words
  labels = []
  for each part in parts
    if (part.style = "poster")
      poster = m.posters[part.text]
      if poster <> invalid
        newPoster = m.top.createChild("Poster")
        newPoster.width = poster.width
        newPoster.height = poster.height
        newPoster.uri = poster.uri
        labels.push(newPoster)
      end if
    else
      label = createLabel(part.text, part.style)
      labels.push(label)
    end if
  end for
  group.appendChildren(labels)
  return group
end function


function getLineParts(text)
  parts = []

  isMore = true
  while isMore and len(text) > 0
    didMatch = false
    for each style in m.regexes
      matches = style.r.match(text)
      if matches.count() = 2 and len(matches[0]) > 0
        parts.push({ text: matches[1], style: style.name })
        text = right(text, len(text) - len(matches[0]))
        didMatch = true
        exit for
      end if
    end for
    if not didMatch
      exit while
    end if
  end while
  if len(text) > 0
    parts.push({ text: text, style: "font" })
  end if
  if parts.count() = 0
    parts.push({ text: " ", style: "font" })
  end if
  return parts
end function

function createLabel(text, name)
  label = createObject("roSGNode", "Label")
  label.font = m[name]
  label.color = m[name + "Color"]
  label.text = text
  return label
end function

function resetView()
  m.posters = {}
  m.contentGroup.removeChildren(m.contentGroup.getChildren(-1, 0))
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ fonts
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function updateFonts(event = invalid)
  if event = invalid
    fontNames = m.fontFields
  else
    fontNames = [event.getField()]
  end if

  for each name in fontNames
    fontSettings = m.top[name + "Settings"]
    m[name] = getFont(fontSettings.name, fontSettings.size)
    m[name + "Color"] = fontSettings.color
  end for

  resetView()
end function

function getFont(name, size)
  font = invalid

  m.measureLabel.font = name
  if m.measureLabel.font <> invalid
    m.measureLabel.font.size = size
    return m.measureLabel.font
  end if
  return m.defaultFont
end function

function setFontDefaults()
  m.top.fontSettings = makeFontSetting("font:MediumSystemFont", 20)
  m.top.header1FontSettings = makeFontSetting("font:MediumBoldSystemFont", 40)
  m.top.header2FontSettings = makeFontSetting("font:MediumSystemFont", 30)
  m.top.header3FontSettings = makeFontSetting("font:MediumSystemFont", 30)
  m.top.italicFontSettings = makeFontSetting("font:MediumBoldSystemFont", 18)
  m.top.boldFontSettings = makeFontSetting("font:MediumBoldSystemFont", 22)
end function

function makeFontSetting(name, size, color = "#000000") 
  return {
    name: name
    size: size
    color: color
  }
end function 

function udpateFontSetting(style, name, size, color = "#000000")
  m[style] = makeFontSetting(name, size, color)
end function