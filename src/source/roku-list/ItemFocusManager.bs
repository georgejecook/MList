import "pkg:/source/roku-list/BaseClass.bs"

class ItemFocusManager extends BaseClass

  public viewManager = invalid
  public component = 0
  public index = 0
  public indexPosition = 0
  public nextIndex = 0
  public nextPosition = 0
  public nextComponent = 0
  public direction = 0
  public didStart = false
  public didFinish = false

  function new(viewManager, moveAnimation, moveAnimationInterpolator)
    super("ItemFocusManager")
    m.viewManager = viewManager
    m.container = viewManager.container
    m.delegate = viewManager.owner
    m.moveAnimation = moveAnimation
    m.moveAnimationInterpolator = moveAnimationInterpolator
    m.observeNodeField(moveAnimationInterpolator, "fraction", m.onAnimationFractionChange, "value", false, m)
    owner = viewManager.owner
    m.delegates = [owner]

    if owner.delegate <> invalid
      m.delegates.push(owner.delegate)
    end if

  end function


  function setFocusPosition(index, indexPosition, nextIndex, nextPosition, direction)
    m.index = index
    m.indexPosition = indexPosition
    m.nextIndex = nextIndex
    m.nextPosition = nextPosition
    m.direction = direction
    m.component = m.viewManager.getRenderer(m.index)
    m.nextComponent = m.viewManager.getRenderer(m.nextIndex)
    m.onFocusUpdate(0)
    m.didStart = false
    m.didFinish = false
  end function

  function onFocusAnimationUpdate(fraction)
    m.onFocusUpdate(fraction)
  end function

  function onContainerPositionChange(offset)
    nextPos = (m.nextPosition - m.indexPosition)
    if nextPos <> 0
      fraction = (offset - m.indexPosition) / nextPos
      m.onFocusUpdate(abs(fraction))
    else
      ? "FIXME"
    end if
  end function

  function onFocusUpdate(fraction) as void
    if fraction = 0 
      if m.didStart
        return
      end if
      name = "onWillGainFocus"
      nextname = "onWillLoseFocus"
    else if fraction = 1
      if m.didFinish
        return
      end if
      name = "onDidGainFocus"
      nextname = "onDidLoseFocus"
    else
      name = "onGainingFocus"
      nextname = "onLosingFocus"
    end if

    event = {
      direction: m.direction
      fraction: fraction
      name: name
      index: m.index
    }
    nextEvent = {
      direction: m.direction
      fraction: fraction
      name: nextName
      index: m.nextIndex
    }

    'let components know, so they can react
    if m.component <> invalid
      m.component.listEvent = event
    end if

    if m.nextComponent <> invalid
      m.nextComponent.listEvent = nextEvent
    end if

    for each delegate in m.delegates
      delegate.listEvent = event
      delegate.listEvent = nextEvent
    end for
  end function

end class